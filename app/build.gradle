plugins {
  id 'application'

  // https://plugins.gradle.org/plugin/com.github.sherter.google-java-format
  id 'com.github.sherter.google-java-format' version '0.9'

  // use to containerize the app
  id 'com.google.cloud.tools.jib' version '2.1.0'
}

application {
  mainClass = 'com.example.App'
}

repositories {
  mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
  implementation 'org.apache.kafka:kafka-streams:2.8.0'

  // configuration
  implementation "com.typesafe:config:1.4.0"

  // logging
  implementation 'ch.qos.logback:logback-classic:1.2.3'

  // test dependencies
  testImplementation 'org.apache.kafka:kafka-streams-test-utils:2.8.0'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
  testImplementation 'org.assertj:assertj-core:3.15.0'
}

// code formatting + error prone
tasks.withType(JavaCompile).configureEach { javaCompile ->
  javaCompile.dependsOn 'googleJavaFormat'
  options.compilerArgs << "-Xlint:deprecation"
  options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jib {
  to {
    image = 'example/myapp:0.1.0'
  }
  extraDirectories.paths = ['files']
  container {
    jvmFlags = [
      // config to use in the Docker container
      '-Dconfig.file=/docker.conf',
    ]
    mainClass = application.mainClass
    labels = [key1:'value1', key2:'value2']
    format = 'OCI'
  }
}
